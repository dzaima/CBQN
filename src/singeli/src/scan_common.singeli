# Used by scan.singeli and bins.singeli

def sel8{v:V, t} = sel{[16]u8, v, make{re_el{i8,V}, t}}
def sel8{v:V, t & w256{V} & istup{t} & length{t}==16} = sel8{v, merge{t,t}}

def shuf{T, v, n & istup{n}} = shuf{T, v, base{4,n}}

local def rev{t} = { def l=length{t}; def j=l-1; select{j-t, j-range{l}} }
local def rev{up,t} = if (up) t else rev{t}
def sel8{v, t, up} = sel8{v, rev{up,t}}

def zip{up, x} = (if (up) zipHi else zipLo){x,x}

# Fill last 4 bytes with last element, in each lane
def spread{a:VT, ...up} = {
  def w = elwidth{VT}
  def b = w/8
  if (w<=16) sel8{a,merge{iota{12},(16-b)+iota{4}%b}, ...up}; else a
}

# Set all elements with the last element of the input
def toLast{n:VT, up & hasarch{'X86_64'} & w128{VT}} = {
  def l{v, w} = l{zip{up,v}, 2*w}
  def l{v, w & hasarch{'SSSE3'}} = sel8{v, up*(16-w/8)+iota{16}%(w/8)}
  def l{v, w==32} = shuf{[4]i32, v, 4**(up*3)}
  def l{v, w==64} = shuf{[4]i32, v, (2*up) + tup{0,1,0,1}}
  l{n, elwidth{VT}}
}
def toLast{n:VT, up & hasarch{'AVX2'} & w256{VT}} = {
  if (elwidth{VT}<=32) sel{[8]i32, spread{n,up}, [8]i32**(up*7)}
  else shuf{[4]u64, n, 4**(up*3)}
}
def toLast{n:VT} = toLast{n, 1}

# Make prefix scan from op and shifter by applying the operation
# at increasing power-of-two shifts
def prefix_byshift{op, sh} = {
  def pre{v:V, k} = if (k < width{V}) pre{op{v, sh{v,k}}, 2*k} else v
  {v:T} => pre{v, if (isvec{T}) elwidth{T} else 1}
}

def get_id{op,T} = (match (op) { {_==min}=>maxvalue; {_==max}=>minvalue }){T}

def make_scan_idem{T, op, up} = {
  # Within each lane, scan using shifts by powers of 2. First k elements
  # when shifting by k don't need to change, so leave them alone.
  def shift{k,l} = rev{up, merge{iota{k},iota{l-k}}}
  def shb{v:V, k} = {
    def w=width{T}; def c = k/w
    def merger{a,b} = if (up) merge{a,b} else merge{b,a}
    def id = make{V, merger{c**get_id{op,T}, (width{V}/w-c)**0}}
    (if (up) shl else shr){[16]u8, v, k/8} | id
  }
  def shb{v, k & hasarch{'SSSE3'}} = sel8{v, shift{k/8,16}}
  def shb{v, k & k>=32} = shuf{[4]u32, v, shift{k/32,4}}
  def shb{v, k & k==128 & hasarch{'AVX2'}} = {
    # After lanewise scan, broadcast end of lane 0 to entire lane 1
    sel{[8]i32, spread{v,up}, make{[8]i32, rev{up,3*(3<iota{8})}}}
  }
  prefix_byshift{op, shb}
}
def make_scan_idem{T==f64, op, up} = {
  def sc{a:T & vcount{T}==2} = op{a, zip{~up,a}}
  def sc{a:T & hasarch{'AVX2'} & w256{T}} = {
    def sh{s, a} = op{a, shuf{[4]u64, a, rev{up,s}}}
    sh{tup{0,1,1,1},sh{tup{0,0,2,2},a}}
  }
  sc
}
def make_scan_idem{T, op} = make_scan_idem{T, op, 1}
